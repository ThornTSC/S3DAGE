// Allow the South and Flicky themes to edit the movement of title card text,
// and load PLCs earlier than base S3&K would.
// Function taken from maingame\hud\titlecard.lemon
//# address-hook(0x02d95c) end(0x02d99a)
function void TitleCard.TextElement.Update()
{
	if (original_mode)
	{
		base.TitleCard.TextElement.Update()
		return
	}

	if (theme.hud == theme.south)
		South.TitleCard.TextElement.Update()
	else if (theme.hud == theme.flicky)
		Flicky.TitleCard.TextElement.Update()
	else
		base.TitleCard.TextElement.Update()

	if (global.zone == 0x0c || global.zone == 0x17)
		return

	// Load PLCs early
	A1 = 0xffff0000 + u16[A0 + 0x48]
	D0 = u16[A1 + 0x32]
	if (D0.u16 == 1 && (objA0.animation.sprite >= 0x04 && objA0.animation.sprite <= 0x11))	// we use the zone name display to load PLCs, as the zone name sets D0 before other text elements do
	{
		addPatternLoadingCue(0x08399a)		// Spike and Springs
		fn02f77c()
		StartLoadingPatternsForZone()
	}
}

// Keep title card centered when screen height is changed
//# address-hook(0x02d76a) end(0x02d802)
function void TitleCard.Wait()
{
	if (kosinski.waiting_modules)
		return

	if (allocDynamicObjectAfterA0())
	{
		if ((global.zone >= 0x13 && global.zone != 0x16 && global.zone_act != 0x1700) || (unlock_act == s3_CGZ))
		{
			// Bonus stage
			A2 = 0x02dab4
			D1 = 1
		}
		else if (u8[A0 + 0x45] != 0)
		{
			// Competition mode stage (unused)
			A2 = 0x02daa6
			D1 = 0
		}
		else
		{
			// Normal stage
			A2 = 0x02da6e
			D1 = 3
		}

		while (D1.s16 >= 0)
		{
			++u16[A0 + 0x30]
			objA1.update_address = u32[A2]
			objA1.render_flags = render_flag.COMPOUND
			u16[A1 + 0x46] = u16[A2+4]
			objA1.position.x.u16 = u16[A2+6]
			objA1.position.y.u16 = u16[A2+8]
			u8[A1 + 0x22] = u8[A2+10]
			objA1.box_size.x = u8[A2+11]
			u8[A1 + 0x28] = u16[A2+12]
			objA1.mapping_offset = 0x02ee10
			u16[A1 + 0x48] = A0.u16
			A2 += 14

			#if STANDALONE
			{
				// Move title card elements towards screen center
				u16 offsetX = getScreenExtend()
				objA1.position.x.u16 += offsetX
                u16 offsetY = getScreenHeightExtend()/2
				objA1.position.y.u16 += offsetY
				if (objA1.update_address != addressof(TitleCard.RedBarElement.Update))
					u16[A1 + 0x46] += offsetX
                else
                    u16[A1 + 0x46] += offsetY
			}
			#endif

			--D1.s16
			if (!allocDynamicObjectAfterA1())
				break
		}

		if (u8[A0 + 0x3e] != 0 && global.zone == 0x06)
		{
			requestLoadingPatterns(0x25)		// LBZ2 object sprites
		}

		objA0.base_state += 2
	}
}

//# address-hook(0x02d856) end(0x02d8dc)
function void TitleCard.Show()
{
	if (objA0.countdown_value != 0)
	{
		--objA0.countdown_value
		return
	}

	if (u16[A0 + 0x30] != 0)
	{
		++objA0.value32
		return
	}

	if (u8[A0 + 0x45] == 0)
	{
		if (global.zone_act == 0x0801)
		{
			// Sandopolis Act 2
			if (allocDynamicObjectStd())
			{
				// Ghost spawner
				objA1.update_address = 0x08f0b8
			}
		}

		if ((global.zone >= 0x13 && global.zone != 0x16 && global.zone_act != 0x1700) || (unlock_act == s3_CGZ))
		{
			// Do nothing for bonus stages and Hidden Palace emerald cave
		}
		else
		{
			if (objA0.value3e != 0)
			{
				level.results_done = 0xff
				if (global.zone_act != 0x1700)
				{
					fn02f77c()
					StartLoadingPatternsForZone()
				}
			}
			else if (global.zone != 0x0c && global.zone_act != 0x1700 && original_mode)
			{
				addPatternLoadingCue(0x08399a)		// Spike and Springs
				fn02f77c()
				StartLoadingPatternsForZone()
			}
		}
	}
	UnloadObject()
}

// Run timers for Sonic 2 title cards outside of their rendering code so they still operate normally during a theme switch mid-title card.
// We also make timing adjustments to the bar element here for the Saturn theme to help keep the title on screen just a bit longer.
// Function taken from maingame\hud\titlecard.lemon
//# address-hook(0x02d8e2) end(0x02d926)
function void TitleCard.RedBarElement.Update()
{
	S2TitleCard.loaded = true
	if (!level.framecounter)	// Level framecounter hasn't moved? Unless we hit the single frame of rollover after 18 minutes of sitting in a level, we are loading a zone fresh.
		S2TitleCard.full = true
	if (timer.alldata == 0x011c)		// 0'01"52 on the HUD timer. We assume it is not possible to enter a special stage or touch a star post in this short window of time.
		S2TitleCard.disappear = true	// If the title card is up with this timer displayed, we are about to end the between-act title card.
	if (!global.game.paused)
	{
		if (!S2TitleCard.disappear)
			objA0.value32++		// run a timer for the Sonic 2 title card entering the screen
		else
			objA0.value3e++		// run a timer for the Sonic 2 title card leaving the screen
	}

	A1 = 0xffff0000 + u16[A0 + 0x48]
	D0.u16 = u16[A1 + 0x32]
	if (D0.u16 != 0)
	{
		// Disappearing
		if (!(objA0.render_flags & render_flag.VISIBLE))
		{
			--u16[A1 + 0x30]
			UnloadObject()
			S2TitleCard.disappear = false
			S2TitleCard.loaded = false
			return
		}

		if (D0.u8 >= u8[A0 + 0x28])
		{
			if (theme.hud == theme.saturn && !original_mode)
			{
				// Copied from Flicky theme's code for triggering things before the text leaves the screen
				u32 timeToEndTitleCard = 0x0000012a		// time at 320px screen width that other title cards end
				if (getScreenWidth() >= 336)				// it takes longer to end at higher screen widths as the text travels farther
					timeToEndTitleCard++
				if (getScreenWidth() >= 400)				// it takes longer to end at higher screen widths as the text travels farther
					timeToEndTitleCard++
				if (getScreenWidth() >= 464)				// it takes longer to end at higher screen widths as the text travels farther
					timeToEndTitleCard++
				if (timer.alldata == timeToEndTitleCard && u8[A1 + 0x3e])	// time that other themes have removed text and ended the title card in an act transition.
					level.results_done = 0xff
			}
			objA0.position.y.u16 -= (theme.hud == theme.saturn && !original_mode) ? 0x02 : 0x20	// slow the retreat of the title card bar in the Saturn theme to keep the zone name legible a bit longer
		}
	}
	else
	{
		// Appearing or showing
		if (objA0.position.y.u16 != u16[A0 + 0x46])
		{
			objA0.position.y.u16 += 0x10
			u8[A1 + 0x34] = 0xff
		}
	}

	objA0.box_size.y = 0x70
	DrawObject()
}
