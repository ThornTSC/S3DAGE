// This function is here only for script mods that want to change the sound key, e.g. to play a custom music track / sound effect

function u64 Standalone.getModdedSoundKey(u64 soundKey, u8 sfxId, u8 soundRegType)
{
    if (unlock_act || challenge_greenspheres)
    {
        soundKey = Standalone.getModdedSoundKey.unlockAct(soundKey, sfxId, soundRegType)
    }
	else if (sfxId == 0x1c && greenspheres_music && !original_mode)
	{
		bool useSKLayouts = (global.lock_on_state != 0 || global.sk_bluespheres)

		if (isMainCharacter(CHARACTER_KNUCKLES) && useSKLayouts && specialstages_knuckles)
			soundKey = "greenspheres_speedup"
	}
	else if (sfxId == 0x2f && ((global.game_mode) & 0x7f) == 0x58)
	{
		soundKey = "mainmenu"
	}
    else if (sfxId == MUSIC_CONTINUE && speedSpheres)				// 0x28
	{
        soundKey = "s2_results"
	}
    else if (sfxId == MUSIC_LEVELCLEAR && speedSpheres)				// 0x29
	{
        soundKey = "s2_actcomplete"
	}
    else if (sfxId == MUSIC_INVINCIBLE && speedSpheres)				// 0x2c
	{
        soundKey = "s2_super"
	}
	else if (sfxId == 0xf8 && greenspheres_music)
	{
		bool useSKLayouts = (global.lock_on_state != 0 || global.sk_bluespheres)
		bool useGeneratedStage = BlueSpheres.useGeneratedStages()
		bool isGeneratedStageInMainGame = (useGeneratedStage && bluespheres.minigame_mode == 0)

		bool KnucklesMainGameRandomGreenSphereStage = (useGeneratedStage && isMainCharacter(CHARACTER_KNUCKLES) && useSKLayouts && specialstages_knuckles)
		
		if ((KnucklesMainGameRandomGreenSphereStage || bluespheres.minigame_mode == 1 || challenge_greenspheres) && green_spheres.active)
			soundKey = "greenspheres_speedup45"
		
		if (speedSpheres)
			soundKey = "s2_specialstage"
	}
	else if (sfxId == 0xdc && original_mode && Audio.getAudioKeyType(MUSIC_CREDITSMEDLEY) == AudioKeyType.REMASTERED)
	{
		soundKey = "sk_credits"
	}
	
	// Return base game or lower mod music
	return base.Standalone.getModdedSoundKey(soundKey, sfxId, soundRegType)
}

function u64 Standalone.getModdedSoundKey.unlockAct(u64 soundKey, u8 sfxId, u8 soundRegType)
{
    // Fast music tracks do not need to be called separately
    if (sfxId == MUSIC_MGZ1)						// 0x05
	{
		if (unlock_act == s2_EHZ)
        	soundKey = (setting.ehz_music) ? "ehz2" : "ehz1"
		else if (unlock_act == s2_CPZ)
			soundKey = "cpz"
		else if (unlock_act == s3_CGZ)
			soundKey = MUSIC_CGZ
	}

	if (unlock_act == s3_CGZ)
		return soundKey

    if (sfxId == MUSIC_MAINBOSS)				// 0x19
        soundKey = "s2_boss"
	else if (sfxId == MUSIC_SPECIALSTAGE && greenspheres_music && !original_mode)	// 0x1c
	{
//		bool useSKLayouts = (global.lock_on_state != 0 || global.sk_bluespheres)

//		if (isMainCharacter(CHARACTER_KNUCKLES) && useSKLayouts && specialstages_knuckles)
			soundKey = "s2_specialstage"	// _speedup
	}
    else if (sfxId == MUSIC_GAMEOVER)				// 0x27
        soundKey = "s2_gameover"
    else if (sfxId == MUSIC_CONTINUE)				// 0x28
        soundKey = "s2_continue"
    else if (sfxId == MUSIC_LEVELCLEAR)				// 0x29
        soundKey = "s2_actcomplete"
//    else if (sfxId == MUSIC_EXTRALIFE)			// 0x2a	// 1UPs are handled in the next function
//        soundKey = "s2_1up"
	else if (sfxId == MUSIC_CHAOSEMERALD)			// 0x2b
		soundKey = "s2_emerald"
    else if (sfxId == MUSIC_INVINCIBLE)				// 0x2c
        soundKey = "s2_invin"
    else if (sfxId == SFX_SUPERTRANSFORM)			// 0x9f
        soundKey = "s2_transform"
    else if (sfxId == SFX_SPRING && global.game_mode != 0x34)					// 0xb1	// do not let yellow spheres use the Sonic 2 sound
        soundKey = "s2_spring"
    else if (sfxId == 0xf0)
        soundKey = "s2_super"
	else if (sfxId == 0xf8 && greenspheres_music)
	{
		soundKey = "s2_specialstage"	// _speedup45
	}
    
    return soundKey
}

// Function from standalone\audio.lemon to implement new audio
// and to enforce most original tracks for Original Mode.
// We also set the 1UP jingle for unlockable acts here.
function void Standalone.playAudio(u8 sfxId)
{
	// Handle music control IDs
	if (sfxId == MUSIC_CTRL_FADEOUT)
	{
		Audio.fadeOutChannel(0, 0x200)	// Fade out over 2 seconds
		return
	}
	else if (sfxId == MUSIC_CTRL_STOP)
	{
		Audio.stopChannel(0)
		return
	}
	else if (sfxId >= 0xe3 && sfxId <= 0xef)
	{
		// Just ignore these
		return
	}

	u64 soundKey = sfxId

	// Choose whether it's music, a jingle or a sound effect
	u8 soundRegType = 0
	if (sfxId <= 0x24)
	{
		soundRegType = SoundRegType.TYPE_MUSIC
	}
	else if (sfxId <= 0x2b)
	{
		soundRegType = (sfxId == 0x26 || sfxId == 0x28) ? SoundRegType.TYPE_MUSIC : SoundRegType.TYPE_JINGLE
	}
	else if (sfxId <= 0x30)
	{
		soundRegType = SoundRegType.TYPE_MUSIC
	}
	else if (sfxId <= 0x32)
	{
		soundRegType = SoundRegType.TYPE_JINGLE
	}
	else if (sfxId <= 0xdb || (sfxId >= 0xdd && sfxId <= 0xe0))
	{
		soundRegType = SoundRegType.TYPE_SOUND
	}
	else
	{
		soundRegType = SoundRegType.TYPE_JINGLE
	}

	// Clear music for level results, emerald jingle, drowning music and outro music
	if (sfxId == 0x29 || sfxId == 0x2b || sfxId == 0x31 || sfxId == 0xdc)
	{
		Audio.stopChannel(0)
	}

	// Check if a script mod wants to change the sound key
	u64 moddedSoundKey = Standalone.getModdedSoundKey(soundKey, sfxId, soundRegType)
	if (moddedSoundKey != soundKey)
	{
		// Sound key got changed by modded function, so don't run the default logic here;
		// instead, ask modded scripts if they want to change the sound reg type as well
		soundKey = moddedSoundKey
		soundRegType = Standalone.getModdedSoundRegType(soundKey, sfxId, soundRegType)
	}
	else
	{
		// Hard-coded handling of special sfx IDs
		if (sfxId == 0x1c)
		{
			// Special Stage
			if (true)	// TODO: Add option to disable the speedup, of course for both music and gameplay
			{
				soundKey = "1c_speedup"
			}
		}
		else if (sfxId == 0x1f && !original_mode)
		{
			// Knuckles' Theme
			if (Game.getSetting(SETTING_AUDIO_KNUCKLES_THEME) == 0)
			{
				soundKey = "1f_sonic3"
			}
			else if (Game.getSetting(SETTING_AUDIO_KNUCKLES_THEME) == 2)
			{
				soundKey = "1f_proto"
			}
			else if (Game.getSetting(SETTING_AUDIO_KNUCKLES_THEME) == 0x10)
			{
				if (!isSonicAndKnucklesZone())
					soundKey = "1f_sonic3"
			}
		}
		else if (sfxId == 0x25 && !original_mode)
		{
			// Title theme
			if (Game.getSetting(SETTING_AUDIO_TITLE_THEME) == 0)
			{
				soundKey = "25_sonic3"
			}
		}
		else if (sfxId == 0x26)
		{
			// Outro music
			if (Game.getSetting(SETTING_AUDIO_OUTRO) == 0 || original_mode)
			{
				soundKey = 0x15			// Sky Sanctuary
			}
			else if (Game.getSetting(SETTING_AUDIO_OUTRO) == 2)
			{
				soundKey = "26_proto"
			}
		}
		else if (sfxId == 0x28 && !original_mode)
		{
			// Continue screen music
			if (Game.getSetting(SETTING_AUDIO_CONTINUE_SCREEN) == 0)
			{
				soundKey = "28_sonic3"
			}
		}
		else if (sfxId == 0x2a)
		{
			// Extra life jingle
            if (unlock_act && (unlock_act != s3_CGZ))
            {
                soundKey = "s2_1up"
            }
			else if (Game.getSetting(SETTING_AUDIO_EXTRALIFE_JINGLE) == 0 && !original_mode)
			{
				soundKey = "2a_sonic3"
			}
			else if (Game.getSetting(SETTING_AUDIO_EXTRALIFE_JINGLE) == 0x10 && !original_mode)
			{
				if (!isSonicAndKnucklesZone())
					soundKey = "2a_sonic3"
			}

			// Special handling for this jingle: Play in channel 1, but mark as override for channel 0 (= music)
			Audio.playOverride(soundKey, AudioContext.CONTEXT_MUSIC, 1, 0)
			return
		}
		else if (sfxId == 0x2c && !original_mode)
		{
			// Invincibility Theme
			if (global.game_mode != 0x2c)	// In Blue Sphere game's Perfect screen, always use the S&K version
			{
				if (Game.getSetting(SETTING_AUDIO_INVINCIBILITY_THEME) == 0)
				{
					soundKey = "2c_sonic3"
				}
				else if (Game.getSetting(SETTING_AUDIO_INVINCIBILITY_THEME) == 0x10)
				{
					if (!isSonicAndKnucklesZone())
						soundKey = "2c_sonic3"
				}
			}
		}
		else if (sfxId == 0x2d)	// can  && !original_mode this, but entering Challenges or Competition is naturally outside of Original Mode
		{
			// Competition Menu music
			if (Game.getSetting(SETTING_AUDIO_COMPETITION_MENU) == 1)
			{
				soundKey = "2d_proto"
			}
		}
		else if ((sfxId == 0x18 || sfxId == 0x2e))
		{
			// Mini-boss music
			soundKey = 0x18
			if (Game.getSetting(SETTING_AUDIO_MINIBOSS_THEME) == 0 && !original_mode)
			{
				soundKey = "2e_sonic3"
			}
			else if (Game.getSetting(SETTING_AUDIO_MINIBOSS_THEME) == 0x10 && !original_mode)
			{
				if (!isSonicAndKnucklesZone())
					soundKey = "2e_sonic3"
			}
		}
		else if (sfxId == 0x33)
		{
			// Alternating ring sound
			alternatingRingSoundState = !alternatingRingSoundState
			if (!alternatingRingSoundState)
			{
				soundKey = 0x34
			}
		}
		else if (sfxId == 0xf0)
		{
			// Super Theme
			soundRegType = SoundRegType.TYPE_MUSIC
			u8 configOption = Game.getSetting(SETTING_AUDIO_SUPER_THEME)
			if (original_mode)
				configOption = 4
			if (configOption == 0 || configOption == 1)
			{
				// Enable the fast level music
				if (configOption == 1)
				{
					Standalone.setFastMusicFlag(FastMusicFlag.SUPER_THEME, true)
				}

				// Continue normal level music, start it if necessary
				if (!Audio.isPlayingAudio(level.default_music.u8) && level.default_music.u8 != sfxId)
				{
					Standalone.playAudio(level.default_music.u8)
					return
				}
			}
			else if (configOption == 2)
			{
				// Sonic 2 super theme
				soundKey = "supertheme_sonic2"
			}
			else if (configOption == 3)
			{
				// Sonic 3 invincibility theme
				soundKey = "2c_sonic3"
			}
			else if (configOption == 4)
			{
				// S&K invincibility theme
				soundKey = 0x2c
			}
			else
			{
				// Unused track from Sonic 3 prototypes
				soundKey = "2e_proto"
			}
		}
		else if (sfxId == 0xf2)
		{
			// Sonic 1 boss music
			soundKey = "sonic1_boss"
			soundRegType = SoundRegType.TYPE_MUSIC
		}
		else if (sfxId == 0xf3)
		{
			// Sonic 2 boss music
			soundKey = "sonic2_boss"
			soundRegType = SoundRegType.TYPE_MUSIC
		}
		else if (sfxId == 0xf4)
		{
			// Knuckles' Theme (S3 version enforced)
			soundKey = "1f_sonic3"
			soundRegType = SoundRegType.TYPE_MUSIC
		}
		else if (sfxId == 0xf5)
		{
			// Knuckles' Theme (S&K version enforced)
			soundKey = 0x1f
			soundRegType = SoundRegType.TYPE_MUSIC
		}
		else if (sfxId == 0xf6)
		{
			// Mini-boss music (S3 version enforced)
			soundKey = "2e_sonic3"
			soundRegType = SoundRegType.TYPE_MUSIC
		}
		else if (sfxId == 0xf7)
		{
			// Mini-boss music (S&K version enforced)
			soundKey = 0x18
			soundRegType = SoundRegType.TYPE_MUSIC
		}
		else if (sfxId == 0xf8)
		{
			// Special Stage (generated ones, with speed-up only every 45 seconds)
			if (true)	// TODO: Add option to disable the speedup, of course for both music and gameplay
			{
				soundKey = "1c_speedup45"
				if (!Game.isModdedSound("1c_speedup45") && Game.isModdedSound("1c_speedup30"))
				{
					// Prefer modded 30 seconds version variant over the non-modded 45 seconds version
					soundKey = "1c_speedup30"
				}
				soundRegType = SoundRegType.TYPE_MUSIC
			}
		}
		else if (sfxId == MUSIC_KNUCKLES_FORCED_PROTO)
		{
			// Knuckles' Theme (S3 prototype version enforced)
			soundKey = "1f_proto"
			soundRegType = SoundRegType.TYPE_MUSIC
		}
		
		// Support for Sonic 3 prototype music
		if (!original_mode)
		{
			if (sfxId == 0x07 && (Game.getSetting(SETTING_CNZ_PROTOTYPE_MUSIC) & 0x01))
			{
				soundKey = "07_proto"
			}
			else if (sfxId == 0x08 && (Game.getSetting(SETTING_CNZ_PROTOTYPE_MUSIC) & 0x02))
			{
				soundKey = "08_proto"
			}
			else if (sfxId == 0x0b && (Game.getSetting(SETTING_ICZ_PROTOTYPE_MUSIC) & 0x01))
			{
				soundKey = "0b_proto"
			}
			else if (sfxId == 0x0c && (Game.getSetting(SETTING_ICZ_PROTOTYPE_MUSIC) & 0x02))
			{
				soundKey = "0c_proto"
			}
			else if (sfxId == 0x0d && (Game.getSetting(SETTING_LBZ_PROTOTYPE_MUSIC) & 0x01))
			{
				soundKey = "0d_proto"
			}
			else if (sfxId == 0x0e && (Game.getSetting(SETTING_LBZ_PROTOTYPE_MUSIC) & 0x02))
			{
				soundKey = "0e_proto"
			}
		}
	}

	if (soundRegType == SoundRegType.TYPE_MUSIC)
	{
		if (Audio.isPlayingAudio(soundKey))
		{
			// Do not restart music if already playing
			//  -> But make sure it gets played at full volume (in case it's fading out)
			Audio.fadeInChannel(0, 0x40)
			return
		}
	}

	Standalone.playRumbleEffectForAudio(sfxId)

	u8 audioContext = (soundRegType != SoundRegType.TYPE_SOUND) ? AudioContext.CONTEXT_MUSIC : AudioContext.CONTEXT_SOUND
	Audio.playAudio(soundKey, audioContext)
}
